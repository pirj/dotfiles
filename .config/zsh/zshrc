# Shell prompt always in the bottom of the screen
tput cup $LINES 0

# Disable annoying flow mode, free up C-S and C-Q
stty -ixon -ixoff

# Prevent overwriting existing files with '> filename', use '>| filename' # (or >!) instead.
# Hat tip http://ruderich.org/simon/config/zshrc
setopt noclobber

# Zsh, please...
unsetopt correct_all

# Credit: https://robots.thoughtbot.com/how-to-use-arguments-in-a-rake-task
# Allow [ or ] whereever you want
unsetopt nomatch

# Ignore duplicate history entries
setopt histignoredups

# Cache completions
autoload -Uz compinit

# Plugins
export ZPLUG_HOME=/usr/local/opt/zplug
source $ZPLUG_HOME/init.zsh
if ! zplug check --verbose; then
  printf "Install? [y/N]: "
  if read -q; then
    echo; zplug install
  fi
fi
zplug zsh-users/zsh-autosuggestions
zplug zsh-users/zsh-history-substring-search
zplug zsh-users/zsh-syntax-highlighting
zplug djui/alias-tips
zplug load

# Better vi mode (hat tip Doug Black)
bindkey -v

# Switch to command mode even if in insert mode initially
history-incremental-search-backward-ins () {
  zle history-incremental-search-backward
  zle -K vicmd
}
zle -N history-incremental-search-backward-ins
bindkey -M viins ^r history-incremental-search-backward-ins
bindkey -M vicmd ^r history-incremental-search-backward
# Accept incremental search for further editing with a single escape
bindkey -M isearch '\e' accept-search
bindkey -M isearch kj accept-search
bindkey -M isearch ^p history-incremental-search-backward
bindkey -M isearch ^n history-incremental-search-forward

bindkey -M vicmd H beginning-of-line
bindkey -M vicmd L end-of-line

# Use ff to complete an autosuggestion
autosuggest-accept-if-exists () {
  if [ $#BUFFER -gt 0 ]; then
    if [ -n "$POSTDISPLAY" ]; then
      zle autosuggest-accept
    else
      BUFFER="$BUFFER[0,$CURSOR]ff$BUFFER[$CURSOR+1,#BUFFER]"
      CURSOR+=2
    fi
  fi
}
zle -N autosuggest-accept-if-exists
bindkey -M viins ff autosuggest-accept-if-exists
bindkey -M vicmd ff autosuggest-accept

# Better undo/redo
bindkey -M vicmd u undo
bindkey -M vicmd U redo

# Switch to command mode. jk or kk or jj are to slow to work with 0.1s timeout
# kjk<CR> to repeat last command
bindkey -M viins kj vi-cmd-mode

# Yank to the end of line with Y
bindkey -M vicmd Y vi-yank-eol

# Unbind annoying named command binding
bindkey -M vicmd -r ':'

# Switch back to fg %1 on ^Z (hat tip Adam Stankiewicz http://sheerun.net/)
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    fg
    zle redisplay
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey -M viins ^Z fancy-ctrl-z
bindkey -M vicmd ^Z fancy-ctrl-z

# Edit command in external editor
autoload -U edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

bindkey -M vicmd K run-help

# Color scheme
[[ -s ~/.config/zsh/colors.sh ]] && source ~/.config/zsh/colors.sh
autoload -U colors && colors

# Prompt

setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats "%b %m%u%c"
zstyle ':vcs_info:*' actionformats "%b %u%c|%a"
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:*' unstagedstr '*'
precmd () { vcs_info }

# Aliases
[[ -s ~/.config/zsh/alias.zsh ]] && source ~/.config/zsh/alias.zsh

# Homebrew github api key
[[ -s ~/.config/zsh/brew-api-key.sh ]] && source ~/.config/zsh/brew-api-key.sh

# Some local settings I don't expose to public
[[ -s ~/.secret/env ]] && source ~/.secret/env

# Many programs change the terminal state, and often do not restore terminal settings on exiting abnormally. To avoid the need to manually reset the terminal, freeze it:
ttyctl -f
