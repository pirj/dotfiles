# Shell prompt always in the bottom of the screen
tput cup $LINES 0

# Disable annoying flow mode, free up C-S and C-Q
stty -ixon -ixoff

# Powerlevel10k instant prompt. Code that may require console input (password prompts, [y/n] etc.)
# must go above this block; everything else may go below.
if [[ -r "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Prevent overwriting existing files with '> filename', use '>| filename' # (or >!) instead.
# Hat tip http://ruderich.org/simon/config/zshrc
setopt noclobber

# Zsh, please...
unsetopt correct_all

# Credit: https://robots.thoughtbot.com/how-to-use-arguments-in-a-rake-task
# Allow [ or ] whereever you want
unsetopt nomatch

# Ignore duplicate history entries
setopt histignoredups

# Plugins
source ~/.zinit/bin/zinit.zsh

zinit ice depth=1; zinit light romkatv/powerlevel10k

export ZSH_AUTOSUGGEST_STRATEGY=(completion history)
zinit light zsh-users/zsh-autosuggestions

zinit light zsh-users/zsh-history-substring-search
# zinit light zdharma/history-search-multi-word

zinit light MichaelAquilina/zsh-you-should-use

zinit light zdharma/fast-syntax-highlighting

zinit ice blockf; zinit light zsh-users/zsh-completions

# fd, better find
zinit ice as"command" from"gh-r" mv"fd* -> fd" pick"fd/fd"
zinit light sharkdp/fd

# exa, replacement for ls
zinit ice lucid from"gh-r" as"program" mv"exa* -> exa"
zinit light ogham/exa

export _Z_DATA=$XDG_CACHE_HOME/z
zinit light rupa/z

# Cache completions
autoload -Uz compinit; compinit

# Direnv
eval "$(direnv hook zsh)"

# Better vi mode (hat tip Doug Black)
bindkey -v

# Switch to command mode even if in insert mode initially
history-incremental-search-backward-ins () {
  zle history-incremental-search-backward
  zle -K vicmd
}
zle -N history-incremental-search-backward-ins
bindkey -M viins ^r history-incremental-search-backward-ins
bindkey -M vicmd ^r history-incremental-search-backward
# Accept incremental search for further editing with a single escape
bindkey -M isearch '\e' accept-search
bindkey -M isearch kj accept-search
bindkey -M isearch ^p history-incremental-search-backward
bindkey -M isearch ^n history-incremental-search-forward

bindkey -M vicmd H beginning-of-line
bindkey -M vicmd L end-of-line

# Use ff to complete an autosuggestion
autosuggest-accept-if-exists () {
  if [ $#BUFFER -gt 0 ]; then
    if [ -n "$POSTDISPLAY" ]; then
      zle autosuggest-accept
    else
      BUFFER="$BUFFER[0,$CURSOR]ff$BUFFER[$CURSOR+1,#BUFFER]"
      CURSOR+=2
    fi
  fi
}
zle -N autosuggest-accept-if-exists
bindkey -M viins ff autosuggest-accept-if-exists
bindkey -M vicmd ff autosuggest-accept

# Better undo/redo
bindkey -M vicmd u undo
bindkey -M vicmd U redo

# Switch to command mode. jk or kk or jj are to slow to work with 0.1s timeout
# kjk<CR> to repeat last command
bindkey -M viins kj vi-cmd-mode

# Yank to the end of line with Y
bindkey -M vicmd Y vi-yank-eol

# Unbind annoying named command binding
bindkey -M vicmd -r ':'

# Switch back to fg %1 on ^Z (hat tip Adam Stankiewicz http://sheerun.net/)
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    fg
    zle redisplay
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey -M viins ^Z fancy-ctrl-z
bindkey -M vicmd ^Z fancy-ctrl-z

# Edit command in external editor
autoload -U edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

bindkey -M vicmd K run-help

clear-scrollback-and-screen () {
  zle clear-screen
  tmux clear-history
  tput cup $LINES 0
  zle redisplay
}
zle -N clear-scrollback-and-screen
bindkey -v '^L' clear-scrollback-and-screen

# Color scheme
autoload -U colors && colors

# Aliases
[[ -s ~/.config/zsh/alias.zsh ]] && source ~/.config/zsh/alias.zsh

# Homebrew github api key
[[ -s ~/.config/zsh/brew-api-key.sh ]] && source ~/.config/zsh/brew-api-key.sh

# Google cloud for MacOS via homebrew
# source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc'
# source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc'

# Many programs change the terminal state, and often do not restore terminal settings on exiting abnormally. To avoid the need to manually reset the terminal, freeze it:
ttyctl -f

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
