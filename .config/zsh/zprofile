[[ $SSH_AGENT_PID -eq "" ]] && eval $(keychain --eval --agents ssh,gpg --quiet ~/.ssh/id_*[^pub])

[[ ! -d "$tmp" ]] && export tmp="/tmp/$USER"
mkdir -p -m 700 "$tmp"

export XDG_CONFIG_HOME="$HOME/.config"
export XDG_DATA_HOME="$HOME/.local/share"
export XDG_CACHE_HOME="$HOME/.cache"

export EDITOR=vi
[[ -s $(which vim) ]] && export EDITOR=vim
[[ -s $(which nvim) ]] && export EDITOR=nvim

# Reduce delay after you hit the <ESC> to 0.2 seconds (hat tip Doug Black)
export KEYTIMEOUT=20

export HISTFILE=~/.zsh_history
export HISTSIZE=10000
export SAVEHIST=10000

# Prevent overwriting existing files with '> filename', use '>| filename' # (or >!) instead.
# Hat tip http://ruderich.org/simon/config/zshrc
setopt noclobber

# Zsh, please...
unsetopt correct_all

. ~/.config/zsh/antigen/antigen.zsh
antigen bundle zsh-users/zsh-syntax-highlighting
antigen bundle zsh-users/zsh-history-substring-search
antigen bundle djui/alias-tips
antigen apply

# Better vi mode (hat tip Doug Black)
bindkey -v
bindkey '^r' history-incremental-search-backward
bindkey -a '^r' history-incremental-search-backward

bindkey -a H beginning-of-line
bindkey -a L end-of-line

# Better undo/redo
bindkey -M vicmd u undo
bindkey -M vicmd U redo

# Switch to command mode. jk or kk or jj are to slow to work with 0.1s timeout
# kjk<CR> to repeat last command
bindkey -M viins kj vi-cmd-mode

# Switch back to fg %1 on ^Z (hat tip Adam Stankiewicz http://sheerun.net/)
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    fg
    zle redisplay
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey -v '^Z' fancy-ctrl-z
bindkey -a '^Z' fancy-ctrl-z

# Color scheme
[[ -s ~/.config/zsh/colors.sh ]] && source ~/.config/zsh/colors.sh
autoload -U colors && colors

# Git prompt
[[ -s /usr/share/git/git-prompt.sh ]] && source /usr/share/git/git-prompt.sh
export GIT_PS1_SHOWDIRTYSTATE=0

# Prompt
function prompt {
  export PROMPT="%n@%{$fg[blue]%}%m %{$fg[yellow]%}%~ %{$fg[green]%}%$(__git_ps1) %{$reset_color%}% %(!.#.$) "
}
autoload -U add-zsh-hook
add-zsh-hook precmd prompt

# Aliases
[[ -s ~/.config/zsh/alias.zsh ]] && source ~/.config/zsh/alias.zsh

export PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting

# Load RVM into a shell session *as a function*
[[ -s "$HOME/.rvm/scripts/rvm" ]] && source "$HOME/.rvm/scripts/rvm"

# Adds Node module (LiveScript, Jasmin) binaries into path
[[ -s "$HOME/node_modules" ]] && export PATH=$PATH:$HOME/node_modules/.bin
